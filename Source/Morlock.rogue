#================================================================================
# Morlock.rogue
# Started August 15, 2021
#================================================================================
$define VERSION "2.2"
$define DATE    "February 11, 2023"

$requireRogue "2.0"

$define ROGUEC_EXE "roguec"

$include "Bootstrap.rogue"
$include Package
$include PackageInfo

uses Console/CommandLineParser
uses Utility/VersionNumber

try
  Morlock.init( System.command_line_arguments )
catch (error:Error)
  local mesg = error->String
  Console.error.println mesg
  System.exit 1
endTry

class Morlock [singleton]
  PROPERTIES
    HOME          : String
    is_dependency = false

  METHODS
    method init( args:String[] )
      local cmd = parse_args( args )

      Bootstrap.configure( cmd )

      if (not cmd//action or cmd//action=="help")
        print_usage
        System.exit 0
      endIf

      perform_action( cmd )

    method perform_action( cmd:Value )
      which (cmd//action)
        case "alias"
          if (cmd//args.is_empty)
            println( (forEach in (File(HOME)/"bin").listing).before_first(".bat") )
            return
          endIf

          local file = File(HOME)/"bin"/(cmd//args[0]->String)
          if (System.is_windows) file += ".bat"

          if (cmd//args.count == 1)
            if (file.exists)
              local content = String(file)
              if (System.is_windows or content.begins_with("#!/bin"))
                println content
              else
                println "<binary file>"
              endIf
            endIf
            return
          endIf

          local writer = file.print_writer
          if (System.is_windows)
            forEach (line in cmd//args from 1)
              writer.print '@'
              writer.println line
            endForEach
            writer.close
          else
            writer.println "#!/bin/sh"
            forEach (line in cmd//args from 1)
              writer.println line
            endForEach
            writer.close
            System.run( "chmod 0766 $"(file.esc) )
          endIf

          return

        case "bootstrap"
          noAction # already handled
          return

        case "create"
          local info = resolve_package( cmd//args.first )
          local script_filepath = "$.rogue" (info.app_name)
          if (File(script_filepath).exists)
            throw error( "Install script '$' already exists."(script_filepath) )
          endIf

          local src =
          @|class $(CLASS_NAME)Package : Package
           |  # Base class Package is defined here:
           |  # https://github.com/brombres/Morlock/blob/main/Source/Package.rogue
           |  PROPERTIES
           |    name = "$(PACKAGE_NAME)"
           |
           |  METHODS
           |    method init
           |      # Specify dependencies like this:
           |      # dependency "brombres/helloworld"
           |      # dependency "brombres/rogue@1.9.2"   # (1.9.2 or better)
           |
           |      # Uncomment to automatically determine releases using GitHub's API.
           |      # scan_repo_releases
           |
           |      # OR explicitly specify one or more release()s:
           |      # release "https://raw.githubusercontent.com/.../xyz-1.0.tar.gz"
           |      # release ...
           |
           |    #method build
           |      # Called by install(). Override as needed. The default implementation
           |      # knows how to build a Rogo-based project but nothing else.
           |      # Typically the only subfolder in the current folder will be the
           |      # unpacked archive.
           |      #   forEach (folder in File(".",&folders,&ignore_hidden).listing)
           |      #     if (File(folder/"build-file-name").exists)
           |      #       archive_folder = folder  # be sure to set this property
           |      #       execute "$ && <build-command>" (cd_cmd(folder))
           |      #       # macOS:   cd_cmd("folder")    -> "cd folder"
           |      #       # Windows: cd_cmd("C:/folder") -> "C: && cd C:/folder"
           |      #     endIf
           |      #   endForEach
           |
           |    method install
           |      # By the time install() is called the following properties are set:
           |      #   version           # "1.0"
           |      #   url               # "https://github.com/.../abc.tar.gz"
           |      #   archive_filename  # "abc.tar.gz"
           |      #
           |      # The current working directory is a temporary build folder.
           |
           |      download
           |      # Typically no need to customize
           |
           |      unpack
           |      # Knows how to unpack .tar.gz (Unix-like only) and .zip (Unix+Windows)
           |
           |      build
           |      # Builds the unpacked archive. See build() above.
           |
           |      install_executable( "**/exe-filename" )
           |      # Copies the file matching the given pattern to bin_folder/"app-name"
           |
           |      # If you have additional support files to install, copy them to
           |      # 'install_folder'.
           |endClass
          src .= replacing( "$(CLASS_NAME)",   info.app_name.capitalized.replacing('-','_') )
          src .= replacing( "$(PACKAGE_NAME)", info.name )

          println "Creating script template '$'."(script_filepath)
          println "$"(script_filepath)
          File( script_filepath ).save( src )
          return

        case "install"
          if (cmd//args.is_empty) throw error( "Package name expected after 'morlock install'." )
          local info = resolve_package( cmd//args.first, &allow_local_script )

          local is_sufficient_version_installed : Logical
          if (info.version)
            local required_v = VersionNumber(info.version)
            forEach (v in info.installed_versions)
              if (VersionNumber(v) >= required_v)
                is_sufficient_version_installed = true
                escapeForEach
              endIf
            endForEach
          endIf

          if (info.version and is_sufficient_version_installed)
            if (is_dependency) return  # all good
            throw error( "$ version $ is already installed."(info.name,info.version) )
          elseIf (not info.version and info.installed_versions.count and is_dependency)
            return
          endIf

          info.fetch_latest_script
          run_script( cmd, info )
          return

        case "link"
          if (cmd//args.is_empty) throw error( "Missing package name after 'morlock link'." )

          contingent
            necessary (cmd//args.count == 2)
            # This handles 'link <launcher> <exe>'.
            # 'link <package-name>' will fall though to the code coming after this 'which'.

            local launcher_name = cmd//args.first->String
            local exe_filepath = File( cmd//args[1]->String ).abs

            necessary (File(exe_filepath).exists and not File(exe_filepath).is_folder)

            local launcher_filepath = HOME/"bin"/launcher_name
            if (System.is_windows) launcher_filepath += ".bat"
            File( launcher_filepath ).delete

            println "Linking $ -> $" (launcher_name,exe_filepath)
            if (System.is_windows)
              File( launcher_filepath ).save( ''@$ %*''(File(File(exe_filepath).conventional_filepath).esc) )
            else
              local link_cmd = "ln -s $ $" (exe_filepath,launcher_filepath)
              execute( link_cmd, &quiet )
            endIf

            return

          endContingent

        case "list"
          println (forEach in installed_packages)
          return

        case "uninstall"
          if (cmd//args.is_empty) throw error( "Package name expected after 'morlock uninstall'." )
          local info = resolve_package( cmd//args.first )
          if (info.version and not info.installed_versions.contains(info.version))
            throw error( "$ version $ is not installed."(info.name,info.version) )
          endIf
          if (not File(info.folder).exists)
            throw error( "$ is not installed."(info.name) )
          endIf
          run_script( cmd, info )
          return

        case "unlink"
          if (cmd//args.is_empty)
            throw error( "Missing package name or launcher name after 'morlock unlink'." )
          endIf

          local arg = cmd//args.first->String
          if (not arg.contains('/'))
            local launcher = arg
            if (System.is_windows) launcher += ".bat"
            launcher = HOME/"bin"/launcher
            if (File(launcher).exists)
              println "Unlinking $" (arg)
              File( launcher ).delete
            else
              println "No such launcher: " + arg
            endIf
            return
            # else fall through to unlink by package name
          endIf

        case "update"
          local args = cmd//args.to_list<<String>>
          if (args.is_empty) args = installed_packages
          forEach (package in args)
            try
              local info = resolve_package( package )
              info.fetch_latest_script
              run_script( cmd, info )
            catch (err:Error)
              local w = Console.width.or_smaller(80)
              println "="*w
              println "ERROR [$]" (package)
              println err->String.indented(2)
              println "="*w
            endTry
          endForEach
          return

      endWhich

      if (cmd//args.is_empty)
        throw error( "Missing package name after 'morlock $'."(cmd//action) )
      endIf
      local info = resolve_package( cmd//args.first )
      run_script( cmd, info )

    method create_build_folder( info:PackageInfo )->String
      local build_folder = "$/$/$/$" (HOME,"build",info.provider,info.app_name)
      if (not File(build_folder).is_folder)
        #println "Creating " + build_folder
        File( build_folder ).delete
        File( build_folder ).create_folder
      endIf
      return build_folder

    method create_folder( path:String, &chown )
      if (not File(path).is_folder)
        if (not File(path).create_folder)
          local error_message = "Unable to create folder: "+path
          if (System.is_windows) throw Error(error_message)
          local cmd = "sudo mkdir -p $" (File(path).esc)
          execute( cmd, error_message )
        endIf

        if (not System.is_windows)
          if (chown)
            # chown the Morlock home folder
            local group = which{ System.is_macos:":admin" || "" }
            local cmd = "chown $$ $" (System.env//USER,group,File(Morlock.HOME).esc)
            if (not Process.run(cmd,&env).success)
              execute( "sudo " + cmd, "Unable to chown Morlock home folder: "+Morlock.HOME )
            endIf
          endIf

          execute( "chmod 755 $"(File(path).esc), &quiet )
        endIf
      endIf

    method error( message:String )->Error
      return Error( message )

    method header
      println "-" * Console.width.or_smaller(80)

    method header( message:String )
      header
      println message
      header

    method installed_packages->String[]
      local packages = Set<<String>>()
      local versions = File( HOME/"packages/*/*/*" ).listing( &folders, &omit_path )
      forEach (v in versions)
        packages.add( File(v).folder )
      endForEach
      return packages.to_list

    method run_script( command:Value, info:PackageInfo )
      local action = command//action->String
      if (action != "install" and not File(info.folder).exists)
        throw error( "Package $ is not installed." (info.name) )
      endIf

      info.prepare_build_folder

      local script_args = info.package_args
      script_args//action = action
      script_args//command = command

      block script_args = script_args.to_json.to_escaped_ascii('"')
        local v_list = File( HOME/"packages/brombres/morlock" ).listing( &folders, &ignore_hidden, &omit_path )
        if (v_list.is_empty)
          println "[INTERNAL ERROR] Morlock has no current version."
          System.exit 1
        endIf
        v_list.sort( (a,b) => VersionNumber(a) > b )
        local v = v_list.first
        local script_launcher_filepath = HOME/"packages/brombres/morlock/$/Source/ScriptLauncher.rogue"(v)
        local package_filepath  = HOME/"packages/brombres/morlock/$/Source/Package.rogue"(v)

        local exe_filename = info.app_name
        if (System.is_windows) exe_filename += ".exe"
        local exe_filepath = info.folder/exe_filename

        local crc32_filepath = info.folder/"source_crc32.txt"
        local crc32 : Int32
        contingent
          # Recompile?
          crc32 = File(info.filepath).crc32 ~ File(package_filepath).crc32 ~ File(script_launcher_filepath).crc32 ~ $rogueVersion.hash_code
          necessary (File(exe_filepath).exists)
          necessary (File(crc32_filepath).exists)
          necessary (crc32->String == String(File(crc32_filepath)).trimmed)

        unsatisfied
          local cmd = "$ $ $ $ --debug --api --main --output=$" ...
            (ROGUEC_EXE,File(info.filepath).esc, File(script_launcher_filepath).esc, ...
            File(package_filepath).esc, File(info.build_folder/info.app_name).esc)
          execute( cmd, &quiet, &exit_on_error )

          local build_filepath = File(info.build_folder/info.app_name).esc
          local local_exe      = File(info.build_folder/exe_filename).esc
          if (System.is_windows)
            cmd = "cl /nologo $.c /Fo$.obj /Fe$ > nul"(build_filepath,build_filepath,local_exe)
          else
            cmd = "cc -Wall -fno-strict-aliasing $.c -o $ -lm"(build_filepath,local_exe)
          endIf
          execute( cmd, &quiet, &exit_on_error )

          File( exe_filepath ).delete
          File( info.build_folder/exe_filename ).copy_to( exe_filepath )
          if (not System.is_windows) execute( "chmod u+x $" (File(exe_filepath).esc), &quiet )
          File( crc32_filepath ).save( crc32->String )

        endContingent

        execute( '$ && $ "$"' (cd_cmd(info.build_folder),File(exe_filepath).esc,script_args), &quiet, &exit_on_error )
      endBlock

    method resolve_package( name:String, &allow_local_script )->PackageInfo
      local info    : PackageInfo
      local version : String

      if (name.contains('@'))
        version = name.after_last('@')
        name .= before_last('@')
      endIf

      if (name.contains("://"))
        info = PackageInfo( name )

      elseIf (File(name).exists and not File(name).is_folder)
        # Local script for install.
        local filepath = name

        if (not allow_local_script)
          throw error( "A local .rogue script can only be used with 'morlock install'." )
        endIf

        info = PackageInfo( String(File(filepath)), &is_script )

        # Copy script into place
        File( info.folder ).create_folder
        File( filepath ).copy_to( info.filepath, &if_different )

      else
        # TODO: map 'name' to any "built-in" packages.
        if (name.contains('/'))
          info = PackageInfo( name )
        else
          local listing = File( HOME/"packages/*/$"(name) ).listing( &folders )
          which (listing.count)
            case 0
              info = PackageInfo( name )
            case 1
              local provider = File(listing.first).parent.filename
              info = PackageInfo( provider/name )
            others
              local builder = String()
              builder.println "Ambiguous app name '$' matches mulitple installed packages:"(name)
              builder.println "  $/$"(File(File(forEach in listing).folder).filename,name)
              throw error( builder )
          endWhich
        endIf

      endIf

      info.version = version

      return info

    method parse_args( args:String[] )->Value
      if (System.is_windows)
        HOME = "%HOMEDRIVE%%HOMEPATH%/AppData/Local/Morlock"
      else
        HOME = "/opt/morlock"
      endIf

      local command = CommandLineParser().
      [
        option( "--dependency", &alias="-d" )
        option( "--home=",      &alias="-h", &default=HOME )
        option( "--installer=", &alias="-i" )
      ].parse( args )

      if (command//args.count)
        command//action = command//args.remove_first
      endIf

      HOME = File( command//options//home ).resolved.filepath
      is_dependency = command//options//dependency->Logical

      return command

    method print_usage
      println "Morlock v$" (VERSION)
      println "$ by Brom Bresenham" (DATE)
      println
      println @|USAGE
               |  morlock <command>
               |
               |COMMANDS
               |  help
               |    Show this help text.
               |
               |  alias <command> <line1> [<line2> ...]
               |    Creates a Mac/Linux shell script or Windows batch file on the Morlock path
               |    that executes the given line or lines of code. Use 'morlock unlink <alias>'
               |    to remove.
               |
               |  create <provider>/<app-name>
               |    For example, `create mygithub/myapp` will create a template install script
               |    `myapp.rogue`. Edit it and move it to a root subfolder called `Morlock/`
               |    (or `morlock/`).
               |
               |  install <package>
               |    'morlock install user/repo/app-name' - installs package user/app-name
               |    'morlock install user/app-name'      - shorthand for user/app-name/app-name
               |
               |  link <package>
               |    Re-links the launchers for the specified package.
               |
               |  link <launcher-name> <exe-filepath>
               |    Links an arbitrary launcher on the Morlock path to an arbitrary executable.
               |    For example: 'morlock link myprog ~/MyProject/Build/myprog.exe'.
               |
               |  list
               |    Shows list of all installed packages.
               |
               |  uninstall <package>
               |    Uninstalls the specified package.
               |
               |  unlink <package-name-or-launcher-name>
               |    Unlinks launchers so they're no longer on the Morlock binpath.
               |
               |  update [package-a [package-b ...]]
               |    Updates listed packages or else all packages, including Morlock, Rogue,
               |    and Rogo.
               |
               |PACKAGE FORMAT
               |  provider/repo/app-name
               |  provider/repo
               |  repo
               |  https://github.com/provider/repo/morlock/app-name.rogue
endClass

routine execute( cmd:String, &suppress_error, &allow_sudo, &quiet, &exit_on_error )->Logical
  if (not quiet) println "> " + cmd
  if (0 == System.run(cmd)) return true

  if (allow_sudo)
    println "Error executing '$'; retrying with 'sudo'."(cmd)
    return execute( "sudo "+cmd, &=suppress_error, &=exit_on_error )
  endIf

  if (suppress_error) return false

  if (exit_on_error) System.exit(1)
  throw Error( "Error executing:\n$"(cmd) )
endRoutine
